repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'

targetCompatibility='1.7'
sourceCompatibility='1.7'

group 'org.freeplane.dpolivaev.mnemonicsetter'
version='0.6'

dependencies {
	testCompile 'junit:junit:4.12',
					'org.hamcrest:hamcrest-all:1.3',
					'org.mockito:mockito-all:1.10.19'  
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'mnemonicsetter'
			groupId = project.group
			version = project.version
			from components.java
			artifact sourceJar
			artifact javadocJar
            pom {
                name = project.name
                description = 'Automatically assigns mnemonics to menu items and toolbar elements'
                url = 'https://github.com/dpolivaev/mnemonicsetter'

                scm {
                    url = 'https://github.com/dpolivaev/mnemonicsetter'
                    connection = 'scm:git:https://github.com/dpolivaev/mnemonicsetter.git'
                    developerConnection = 'scm:git:git@github.com:dpolivaev/mnemonicsetter.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'dpolivaev'
                        name = 'Dimitry Polivaev'
                        email = 'dpolivaev@gmx.de'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
        maven { 
			 url  = "${project.gradle.gradleUserHomeDir}/local-artifacts"
		}
    }
}

signing {
    sign publishing.publications.maven
}

